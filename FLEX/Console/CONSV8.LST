
                      ;* ELEKTOR EC6809 CONSOLE I/O DRIVER
                      ;* 11/2023 PH. ROEHR

 EF80                 VIA     EQU     $EF80
 C700                 CHPR    EQU     $C700       

                      ;* IN CASE OF INTERRUPT THE ASSIST-09 EPROM
                      ;* JUMP AT THE ADDRESS STORED AT
 DFC8                 JSWIVE  EQU     $DFC2+6    ; FOR SWI3
 DFCE                 JIRQVE  EQU     $DFC2+12   ; FOR IRQ

 F033                 WTLDVI  EQU     $F033      ; GET // DATA INPUT
 F036                 INIT    EQU     $F036      ; INIT OF VIA, ACIA, CRTC     
   
 F039                 VIARDY  EQU     $F039      ; TEST // DATA READY        
 F03C                 JMPADR  EQU     $F03C      ; PUT CHAR ON SCREEN
 F03F                 GDTVIE  EQU     $F03F      ; GET // DATA INPUT WITH ECHO
 F01B                 ERTS    EQU     $F01B      ; SIMPLE RTS 

 CD00                 COLDS   EQU     $CD00

                              ORG     $D3E5

 D3E5 D370            INCHN   FDB     JINCHN     ; INPUT CHARACTER WITHOUT 
ECHO
 D3E7 D3B1            IHNDLR  FDB     JIHNDLR    ; IRQ INTERRUPT HANDLER
 D3E9 DFC8            SWIVEC  FDB     JSWIVE     ; SWI3 VECTOR LOCATION
 D3EB DFCE            IRQVEC  FDB     JIRQVE     ; IRQ VECTOR LOCATION
 D3ED D3AB            TMOFF   FDB     JTMOFF     ; TIMER OFF ROUTINE
 D3EF D3A5            TMON    FDB     JTMON      ; TIMER ON ROUTINE
 D3F1 D38C            TMINT   FDB     JTMINT     ; TIMER INITIALIZATION
 D3F3 D38A            MONITR  FDB     JMONIT     ; MONITOR ENTRY ADDRESS
 D3F5 F01B            TINIT   FDB     ERTS       ; TERMINAL INITIALIZATION
 D3F7 D383            STAT    FDB     JSTAT      ; CHECK TERMINAL STATUS
 D3F9 D37C            OUTCH   FDB     JOUTCH     ; OUTPUT CHARACTER
 D3FB D376            INCH    FDB     JINCH      ; INPUT CHARACTER WITH ECHO

                              ORG     $D370

                      ;* INPUT CHARACTER WITHOUT ECHO

 D370 BDF033          JINCHN  JSR     WTLDVI   
 D373 847F                    ANDA    #$7F       ; CLEAR PARITY BIT
 D375 39                      RTS

                      ;* INPUT CHARACTER WITH ECHO

 D376 BDF03F          JINCH   JSR     GDTVIE
 D379 847F                    ANDA    #$7F       ; CLEAR PARITY BIT
 D37B 39                      RTS

                      ;* OUTPUT CHAR

 D37C 347F            JOUTCH  PSHS    A,B,CC,DP,X,Y,U
 D37E BDF03C                  JSR     JMPADR
 D381 35FF                    PULS    A,B,CC,DP,X,Y,U,PC

                      ;* CHECK VIA STATUS (IF CHAR IS WAITING Z=1, ELSE Z=0)   

                         
 D383 3402            JSTAT   PSHS    A
 D385 BDF039                  JSR     VIARDY
 D388 3582                    PULS    A,PC 
                              
                      ;* MONITOR ENTRY ADDRESS

 D38A 3F              JMONIT  SWI             
 D38B 08                      FCB     $08
                              
                      ;* TIMER INITIALIZATION    
                          
 D38C 8640            JTMINT  LDA    #$40        
 D38E B7EF8D                  STA    VIA+$0D     ; CLEAR INTERRUPT FLAG 
REGISTER                         
 D391 CC3A98                  LDD    #15000      ; 15 MS INTERRUPTS            
 
 D394 F7EF84                  STB    VIA+$04     ; WRITE LOW ORDER LATCH       
 
 D397 B7EF85                  STA    VIA+$05     ; WRITE HIGH ORDER COUNTER
 D39A B6EF8B                  LDA    VIA+$0B
 D39D 847F                    ANDA   #%01111111  ; TIMER 1 NO OUTPUT ON PB7
 D39F 8A40                    ORA    #%01000000  ; T1 CONTINUOUS INTERRUPT 
MODE
 D3A1 B7EF8B                  STA    VIA+$0B     ; START T1
 D3A4 39                      RTS        
                              
                      ;* TIMER ON ROUTINE    
                          
 D3A5 86C0            JTMON   LDA    #%11000000  ; $C0 ENABLE T1 IRQ        
 D3A7 B7EF8E                  STA    VIA+$0E        
 D3AA 39                      RTS        
                              
                      ;* TIMER OFF ROUTINE  
                           
 D3AB 8640            JTMOFF  LDA    #%01000000  ; $40 DISABLE T1 IRQ       
 D3AD B7EF8E                  STA    VIA+$0E       
 D3B0 39                      RTS       

                      ;* IRQ INTERCEPTION    
                          
 D3B1 3402            JIHNDLR PSHS   A           ; SAVES ON STACK
 D3B3 B6EF8D                  LDA    VIA+$0D     ; GET VIA INTERRUPT FLAG 
REGISTER
 D3B6 48                      ASLA 
 D3B7 48                      ASLA               ; GET BIT 6 INTO CARRY
 D3B8 240A                    BCC    NSPOOL      ; BRANCH IF CARRY NOT SET    
 D3BA 8640                    LDA    #%01000000  ; $40 
 D3BC B7EF8D                  STA    VIA+$0D     ; CLEAR INTERRUPT FLAG 
REGISTER BIT 6
 D3BF 3502                    PULS   A           ; RESTORE STACK
 D3C1 7EC700                  JMP    CHPR        ; JUMP TO SPOOLER
 D3C4 3502            NSPOOL  PULS   A           ; RESTORE STACK
 D3C6 3B                      RTI

                      ;* SET VERIFY OFF
                                      ORG    $D435
 D435 00                              FCB    $00
                                      
                      ;* SET PARAMETERS
                              ORG    $CC00
                      ;*   TTYSET PARAMETERS        
 CC00 08183A                  FCB    $08,$18,$3A
 CC03 185018                  FCB    $18,$50,$18
 CC06 000800                  FCB    $00,$08,$00
 CC09 591B                    FCB    $59,$1B
                      ;*   ASN PARAMETERS       
 CC0B 0001                    FCB    $00,$01
                              
 CD00                         END    COLDS

SYMBOL TABLE
      CHPR 00 C700     COLDS 00 CD00      ERTS 00 F01B    GDTVIE 00 F03F
    IHNDLR 02 D3E7      INCH 02 D3FB     INCHN 02 D3E5      INIT 00 F036
    IRQVEC 02 D3EB   JIHNDLR 02 D3B1     JINCH 02 D376    JINCHN 02 D370
    JIRQVE 00 DFCE    JMONIT 02 D38A    JMPADR 00 F03C    JOUTCH 02 D37C
     JSTAT 02 D383    JSWIVE 00 DFC8    JTMINT 02 D38C    JTMOFF 02 D3AB
     JTMON 02 D3A5    MONITR 02 D3F3    NSPOOL 02 D3C4     OUTCH 02 D3F9
      STAT 02 D3F7    SWIVEC 02 D3E9     TINIT 02 D3F5     TMINT 02 D3F1
     TMOFF 02 D3ED      TMON 02 D3EF       VIA 00 EF80    VIARDY 00 F039
    WTLDVI 00 F033
33 SYMBOLS

0 error(s), 0 warning(s)
